grammar de.redoxi.ruste.Rust 

generate rust "http://www.redoxi.de/ruste/grammar/Rust"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// Top level crate and its implicit module
Crate:
	items+=ItemAndAttrs*
;

// Attribute for an item
ItemAttr:
	'#[' attrs+=Attr (',' attrs+=Attr)* ']'
;

// Individual attribute within an item
Attr:
	LiteralAttr |
	AttrWithList
;

// Attribute with a list of other attributes
AttrWithList:
	ident=IDENT '(' attrs+=Attr (',' attrs+=Attr)* ')'
;

// An attribute with a literal value
LiteralAttr:
	ident=IDENT '=' value=Literal
;

// An item and any associated attributes
ItemAndAttrs:
	attrs+=ItemAttr* item=Item
;

// Items within a module or crate
Item:
	ModItem | FnItem | TypeItem | StructItem | EnumItem | TraitItem | ImplItem | ExternBlock | StaticItem
;

// A module declaration item
ModItem:
	"mod" ident=IDENT (externalBody?=';' | '{' items+=ItemAndAttrs* '}')
;

// Function item
FnItem:
	"fn" ident=IDENT ('<' params += GenericParamDecl (',' params += GenericParamDecl)* '>')? '(' (args += Arg (',' args += Arg)*)? ')' ("->" returnType = Type)? body = Block
;

// TODO Reference Type?
TypeItem:
	"type" ident=IDENT ('<' params+=GenericParamDecl (',' params+=GenericParamDecl)* '>')? '=' type=Type ';'
;

StructItem:
	"struct" ident=IDENT ('<' params+=GenericParamDecl (',' params+=GenericParamDecl)* '>')? '{' fields+=StructField (',' fields+=StructField)* '}'
;

EnumItem:
	"enum" ident=IDENT ('<' params+=GenericParamDecl (',' params+=GenericParamDecl)* '>')? "{" variants+=EnumVariant ("," variants+=EnumVariant)* "}"
;

EnumVariant:
	ident=IDENT ('<' params+=IDENT (',' params+=IDENT)* '>')? ("(" types+=Type ("," types+=Type)* ")" | "{" fields+=StructField ("," fields+=StructField)* /* TODO Wildcard pattern? */ "}")?
;

TraitItem:
	"trait" ident=IDENT ('<' params+=GenericParamDecl (',' params+=GenericParamDecl)* '>')? "{" (methods+=TraitMethod)* "}"
;

TraitMethod:
	(unsafe?="unsafe" | extern?="extern")? vis=Visibility? "fn" ident=IDENT ('<' params+=GenericParamDecl (',' params+=GenericParamDecl)* '>')? '(' (args+=Arg (',' args+=Arg)*)? ')' ("->" returnType=Type)? (body=Block | ';')
;

// TODO Crossreference struct and trait
ImplItem:
	"impl" (trait=IDENT "for")? struct=IDENT "{" (methods+=ImplMethod)* "}"
;

ImplMethod:
	(unsafe?="unsafe" | extern?="extern")? "fn" ident=IDENT ('<' params+=GenericParamDecl (',' params+=GenericParamDecl)* '>')? '(' (args+=Arg (',' args+=Arg)*)? ')' ("->" returnType=Type)? body=Block
;

ExternBlock:
	{ExternBlock} "extern" abi=STRING_LIT? "{" (functions += ForeignFn)* "}" 
;

ForeignFn:
	"fn" ident=IDENT "(" (args+=Arg ("," args+=Arg)*)? ")" ("->" returnType=Type)? ";"
;

StaticItem:
	"static" mutable?=MUT_KEYWORD ident=IDENT (":" type=Type)? "=" expr=Expr ";"
;

Expr:
	ExprLValue |
	ExprRValue
;

ExprLValue:
	ExprPath
;

ExprRValue:
	ExprLiteral |
	ExprTuple |
	ExprStruct |
	// TODO ExprField
	// TODO ExprMethodCall
	ExprVec |
	// TODO ExprIndex
	ExprUnary
;

ExprLiteral:
	literal=Literal
;

ExprPath:
	path=Path ({ExprStruct.path=current} struct=ExprStruct | {ExprTuple.path=current} tuple=ExprTuple)?
;

ExprTuple:
	'(' (exprs+=Expr ',')+ ')'
;

ExprStruct:
	"{" fields+=StructField ("," fields+=StructField)* (".." baseExpr = Expr)? "}"
;

ExprVec:
	'[' mutable?=MUT_KEYWORD (exprs+=Expr ',' (('..' lenExpr=Expr) | exprs+=Expr (',' exprs+=Expr)+))? ']'
;

ExprUnary:
	NumericNegation |
	Dereference |
	LogicalNegation |
	ManagedBox |
	OwnedBox |
	Borrow
;

NumericNegation:
	"-" expr = Expr
;

Dereference:
	"*" expr = Expr
;

LogicalNegation:
	"!" expr = Expr
;

ManagedBox:
	"@" expr = Expr
;

OwnedBox:
	"~" expr = Expr
;

Borrow:
	"&" expr = Expr
;

Block:
	{Block} '{' /* TODO Statements, expression */ '}'
;

// TODO Crossreference Trait?
GenericParamDecl:
	ident=IDENT (':' bounds+=IDENT ('+' bounds+=IDENT))?
;

// TODO Crossreference Type
Arg:
	pat=Pat ':' type=Type
;

// TODO Expand
// See line 2869 of libsyntax/parse/parser.rs
Pat:
	PatWildcard |														
	PatIdent |
	PatBoxed |
	PatOwned |
	PatBorrowed |
	PatTuple |
	PatVector |
	PatLiteral |
	PatRange |
	PatEnum
;

PatWildcard:
	{PatWildcard} '_'
;

PatIdent:
	mutable?=MUT_KEYWORD ident=IDENT
;

PatBoxed:
	"@" pattern=Pat
;

PatOwned:
	"~" pattern=Pat
;

PatBorrowed:
	"&" pattern=Pat
;

PatTuple:
	'(' patterns+=Pat (',' patterns+=Pat)* ')'
;

PatVector:
	'[' patterns+=Pat (',' patterns+=Pat)* ']'
;

PatLiteral:
	literal=Literal
;

PatRange:
	PatCharRange |
	PatNumberRange
;

PatCharRange:
	start=CharLit '..' end=CharLit
;

PatNumberRange:
	start=NumberLit '..' end=NumberLit
;

PatEnum:
	path=Path ({PatTupleEnum.path=current} '(' ('*' | '..' | patterns+=Pat (',' patterns+=Pat)*) ')' | {PatStructEnum.path=current} '{' fieldPatterns+=FieldPat (',' fieldPatterns+=FieldPat)* '}')
;

FieldPat:
	ident=IDENT (':' pattern=Pat)?
;

Path:
	segments+=IDENT ('::' segments+=IDENT)* '::'? ('<' (lifetimes+=LIFETIME | genericTypes+=Type) (',' (lifetimes+=LIFETIME | genericTypes+=Type))* '>')?
;

terminal LIFETIME:
	"'" IDENT
;

Type:
	PrimitiveType |
	TupleType |
	StructType |
	EnumType |
	BoxedPointer |
	OwnedPointer |
	BorrowedPointer
;

PrimitiveType:
	{IntType} INT_TYPE |
	{FloatType} FLOAT_TYPE |
	{BoolType} BOOLEAN_TYPE |
	{MachineType} MACHINE_TYPE |
	{UnitType} UNIT_TYPE
;

TupleType:
	'(' types+=Type (',' types+=Type)* ')'
;

// TODO Move generics to own rule, but attach here
StructType:
	"struct" ident=IDENT ('<' params += GenericParamDecl (',' params += GenericParamDecl)* '>')? '{' fields+=StructField (',' fields+=StructField)* '}'
;

BoxedPointer:
	"@" type=Type
;

OwnedPointer:
	"~" type=Type
;

BorrowedPointer:
	"&" type=Type
;

StructField:
	vis=Visibility? ident=IDENT ':' type=Type
;

EnumType:
	"enum" ident=IDENT ('<' params += GenericParamDecl (',' params += GenericParamDecl)* '>')? '{' variants+=Variant (',' variants+=Variant)* '}'
;

Variant:
	StructVariant | TupleVariant | UnitVariant
;

StructVariant:
	ident=IDENT ('<' params += GenericParamDecl (',' params += GenericParamDecl)* '>')? '{' fields+=StructField (',' fields+=StructField)* '}'
;

TupleVariant:
	ident=IDENT '(' types+=Type (',' types+=Type)* ')'
;

UnitVariant:
	ident=IDENT
;

enum Visibility:
	PRIVATE="priv" | PUBLIC="pub"	
;

terminal INT_TYPE: INT_SIZE;
terminal fragment INT_SIZE: SIGNED_INT_TYPE | UNSIGNED_INT_TYPE;
terminal fragment SIGNED_INT_TYPE: I8 | I16 | I32 | I64;
terminal fragment I8: "i8";
terminal fragment I16: "i16";
terminal fragment I32: "i32";
terminal fragment I64: "i64";
terminal fragment UNSIGNED_INT_TYPE: U8 | U16 | U32 | U64;
terminal fragment U8: "u8";
terminal fragment U16: "u16";
terminal fragment U32: "u32";
terminal fragment U64: "u64";
terminal FLOAT_TYPE: FLOAT_SIZE;
terminal fragment FLOAT_SIZE: F32 | F64;
terminal fragment F32: "f32";
terminal fragment F64: "f64";
terminal BOOLEAN_TYPE: "bool";
terminal MACHINE_TYPE: MACHINE_INT_TYPE | MACHINE_UINT_TYPE;
terminal fragment MACHINE_INT_TYPE: "int";
terminal fragment MACHINE_UINT_TYPE: "uint";
terminal UNIT_TYPE: "()";

terminal MUT_KEYWORD: 'mut';

StringLit: 
	value = STRING_LIT
	// | {RawStringLit} 'r' '#' body = RawString? '#'
;

// Literal value
Literal:
	NumberLit | CharLit | StringLit
;

NumberLit:
	value = (FLOAT_LIT | INT_LIT)
;

// Single character literal
CharLit:
	value = CHAR_LIT
;

terminal CHAR_LIT: "'" (('\\' ('\\' | 'n' | 'r' | 't' | '0')) | UNICODE_CHAR | !('\\' | "'"))* "'";
terminal STRING_LIT: '"' (('\\' ('\\' | 'n' | 'r' | 't' | '0')) | UNICODE_CHAR | !('\\' | '"'))* '"';

terminal fragment UNICODE_CHAR: UTF8_CHAR | UTF16_CHAR | UTF32_CHAR;
terminal fragment UTF8_CHAR: '\\x' HEX_DIGIT HEX_DIGIT;
terminal fragment UTF16_CHAR: '\\u' HEX_DIGIT HEX_DIGIT HEX_DIGIT HEX_DIGIT;
terminal fragment UTF32_CHAR: '\\U' HEX_DIGIT HEX_DIGIT HEX_DIGIT HEX_DIGIT HEX_DIGIT HEX_DIGIT HEX_DIGIT HEX_DIGIT;

// Floating point literal
terminal FLOAT_LIT: DEC_DIGIT (DEC_DIGIT | '_')* ('.' (DEC_DIGIT | '_')+) (('E' | 'e') ('+' | '-')? (DEC_DIGIT | '_')+)? FLOAT_SIZE?;

// Integer literal
terminal INT_LIT: (DEC_INT_LIT | BIN_INT_LIT | OCT_INT_LIT | HEX_INT_LIT) INT_SIZE?;

// Decimal (base 10) integer
terminal fragment DEC_INT_LIT: DEC_DIGIT (DEC_DIGIT | '_')*;

// Binary (base 2) integer
terminal fragment BIN_INT_LIT: "0b" ('0' | '1' | '_')+;

// Octal (base 8) integer
terminal fragment OCT_INT_LIT: "0o" (OCT_DIGIT | '_')+;

// Hex (base 16) integer
terminal fragment HEX_INT_LIT: "0x" (HEX_DIGIT | '_')+;

terminal fragment OCT_DIGIT: '0'..'7';
terminal fragment DEC_DIGIT: '0'..'9';
terminal fragment HEX_DIGIT: '0'..'9' | 'a'..'f' | 'A'..'F';

// terminal fragment NON_SPECIAL_CHAR: !('"' | '\'' | '\\' | 'n' | 'r' | 't' | '0');

terminal fragment XID_START: 'a'..'z'; // TODO Expand
terminal fragment XID_CONTINUE: 'a'..'z' | '_'; // TODO Expand
terminal IDENT: XID_START XID_CONTINUE*;


