grammar de.redoxi.ruste.Rust 

generate rust "http://www.redoxi.de/ruste/grammar/Rust"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// Top level crate and its implicit module
Crate:
	items+=ItemAndAttrs*
;

// Attribute for an item
ItemAttr:
	'#[' attrs+=Attr (',' attrs+=Attr)* ']'
;

// Individual attribute within an item
Attr:
	LiteralAttr |
	AttrWithList
;

// Attribute with a list of other attributes
AttrWithList:
	ident=IDENT '(' attrs+=Attr (',' attrs+=Attr)* ')'
;

// An attribute with a literal value
LiteralAttr:
	ident=IDENT '=' value=Literal
;

// An item and any associated attributes
ItemAndAttrs:
	attrs+=ItemAttr* item=Item
;

// Items within a module or crate
Item:
	ModItem // | FnItem | TypeItem | StructItem | EnumItem | StaticItem | TraitItem | ImplItem | ExternBlock
;

// A module declaration item
ModItem:
	"mod" ident=IDENT (';' | '{' /* body = Mod */ '}')
;

// Literal value
Literal:
	CharLit | NumberLit // TODO | StringLit | NumberLit
;

// Single character literal
CharLit:
	'\'' (char = NON_SINGLE_QUOTE | '\\' (char = '\'' | escapedChar = EscapedChar)) '\''
;

// Escaped character
EscapedChar:
	{EscapedChar} char = ('\\' | 'n' | 'r' | 't' | '0') |
	{UnicodeChar} 'x' (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) |
	{UnicodeChar} 'u' (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) |
	{UnicodeChar} 'U' (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) (digits+=HEX_DIGIT) (digits+=HEX_DIGIT)
;

// Numeric literal
// TODO Represent negatives here?
NumberLit:
	FloatLit | IntLit
;

// Floating point literal
FloatLit:
	digits+=DEC_DIGIT (digits+=DEC_DIGIT | '_')* ('.' (digits+=DEC_DIGIT | '_')+) (('E' | 'e') ('+' | negativeExp?='-')? (digits+=DEC_DIGIT | '_')+)? ('f' size=FloatSize)?
;

// Valid sizes for floats
enum FloatSize: FLOAT="32" | DOUBLE="64";

// Integer literal
IntLit:
	DecIntLit | BinIntLit | OctIntLit | HexIntLit
;

// Decimal (base 10) integer
DecIntLit:
	digits+=DEC_DIGIT (digits+=DEC_DIGIT | '_')* (('i' | unsigned?='u') size=IntSize)?
;

// Binary (base 2) integer
BinIntLit:
	{BinIntLit} "0b" (digits+='0' | digits+='1' | '_')+ (('i' | unsigned?='u') size=IntSize)?
;

// Octal (base 8) integer
OctIntLit:
	{OctIntLit} "0o" (digits+=OCT_DIGIT | '_')+ (('i' | unsigned?='u') size=IntSize)?
;

// Hex (base 16) integer
HexIntLit:
	{HexIntLit} "0x" (digits+=HEX_DIGIT | '_')+ (('i' | unsigned?='u') size=IntSize)?
;

// Valid integer sizes in bits
enum IntSize: BYTE="8" | SHORT="16" | INT="32" | LONG="64";

terminal OCT_DIGIT: '0'..'7';
terminal DEC_DIGIT: '0'..'9';
terminal HEX_DIGIT: '0'..'9' | 'a'..'f' | 'A'..'F';

terminal fragment XID_START: 'a'..'z'; // TODO Expand
terminal fragment XID_CONTINUE: 'a'..'z' | '_'; // TODO Expand
terminal IDENT: XID_START XID_CONTINUE*;

terminal NON_SINGLE_QUOTE: !'\'';